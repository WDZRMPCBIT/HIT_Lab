Module -> epsilon | Defination ; Module | Assignment ; Module | Control Module | Function Module | Struct Module | Call ; Module

Defination -> Data id
Data -> Type Point | Data [ const ]
Type -> int | float | bool | id
Point -> epsilon | * Point

Struct -> struct id { Statement }
Statement -> Defination ; | Defination ; Statement

Function -> Data id ( Parameter ) { Process }
Parameter -> epsilon | Defination | Defination , Parameter
Process -> Module Return
Return -> return Value ; | epsilon

Assignment -> id Index Attribute = Value
Value -> Value + Value | Value - Value | Value * Value | Value / Value | Call
Value -> - Value
Value -> ( Value )
Value -> const | id Index Attribute
Index -> [ Value ] Index | epsilon
Attribute -> epsilon | Attribute . id

Control -> If | IfElse | While
If -> if ( Condition ) { Module }
IfElse -> if ( Condition ) { Module } else { Module }
While -> while ( Condition ) { Module }
Condition -> Condition and Condition | Condition or Condition | not Condition | ( Condition ) | Value Relop Value | true | false
Relop -> < | <= | == | != | > | >=

Call -> id ( Transmit )
Transmit -> epsilon | Value | Value , Transmit

end

以空格分隔字符
非终结符以大写开头
end标识语法结尾

一行只能定义一个变量
定义时不能进行初始化
数组长度只能用常量初始化，格式为type (*) [const]
布尔真假用true和false表示
